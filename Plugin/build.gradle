plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'net.kyori.blossom' version '1.3.1'
    id 'java-library'
    id 'xyz.kyngs.libby.plugin' version '1.0.0'
}

compileJava {
    options.encoding('UTF-8')
}

repositories {
    // mavenLocal()
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://repo.opencollab.dev/maven-snapshots/" }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://hub.spigotmc.org/nexus/" }
    maven { url = "https://mvn.exceptionflug.de/repository/exceptionflug-public/" }
    maven { url = "https://repo.dmulloy2.net/repository/public/" }
    maven { url = "https://repo.alessiodp.com/releases/" }
    maven { url = "https://jitpack.io/" }
    maven { url = "https://repo.kyngs.xyz/public/" }
}

blossom {
    replaceToken '@version@', version
}

shadowJar {
    archiveFileName = "LibreLogin.jar"

    dependencies {
        exclude(dependency('org.slf4j:.*:.*'))
        exclude(dependency('org.checkerframework:.*:.*'))
        exclude(dependency('com.google.errorprone:.*:.*'))
        exclude(dependency('com.google.protobuf:.*:.*'))
    }

    relocate 'co.aikar.acf', 'xyz.kyngs.librelogin.lib.acf'
    relocate 'com.github.benmanes.caffeine', 'xyz.kyngs.librelogin.lib.caffeine'
    relocate 'com.typesafe.config', 'xyz.kyngs.librelogin.lib.hocon'
    relocate 'com.zaxxer.hikari', 'xyz.kyngs.librelogin.lib.hikari'
    relocate 'org.mariadb', 'xyz.kyngs.librelogin.lib.mariadb'
    relocate 'org.bstats', 'xyz.kyngs.librelogin.lib.metrics'
    relocate 'org.intellij', 'xyz.kyngs.librelogin.lib.intellij'
    relocate 'org.jetbrains', 'xyz.kyngs.librelogin.lib.jetbrains'
    relocate 'io.leangen.geantyref', 'xyz.kyngs.librelogin.lib.reflect'
    relocate 'org.spongepowered.configurate', 'xyz.kyngs.librelogin.lib.configurate'
    relocate 'net.byteflux.libby', 'xyz.kyngs.librelogin.lib.libby'
    relocate 'org.postgresql', 'xyz.kyngs.librelogin.lib.postgresql'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    from '../LICENSE.txt'
}

libby {
    excludeDependency 'org.slf4j:.*:.*'
    excludeDependency 'org.checkerframework:.*:.*'
    excludeDependency 'com.google.errorprone:.*:.*'
    excludeDependency 'com.google.protobuf:.*:.*'
}

dependencies {
    //API
    implementation project(':API')

    //Velocity
    annotationProcessor 'com.velocitypowered:velocity-api:3.2.0-SNAPSHOT'
    compileOnly 'com.velocitypowered:velocity-api:3.2.0-SNAPSHOT'
    compileOnly 'com.velocitypowered:velocity-proxy:3.2.0-SNAPSHOT-277'

    //MySQL
    libby 'org.mariadb.jdbc:mariadb-java-client:3.2.0'
    libby 'com.zaxxer:HikariCP:5.0.1'

    //SQLite
    libby 'org.xerial:sqlite-jdbc:3.43.0.0'

    //PostgreSQL
    libby 'org.postgresql:postgresql:42.6.0'

    //ACF
    libby 'com.github.kyngs.commands:acf-velocity:7d5bf7cac0'
    libby 'com.github.kyngs.commands:acf-bungee:7d5bf7cac0'
    libby 'com.github.kyngs.commands:acf-paper:7d5bf7cac0'

    //Utils
    libby 'com.github.ben-manes.caffeine:caffeine:3.1.8'
    libby 'org.spongepowered:configurate-hocon:4.1.2'
    libby 'at.favre.lib:bcrypt:0.10.2'
    libby 'dev.samstevens.totp:totp:1.7.1'
    compileOnly 'dev.simplix:protocolize-api:2.3.1'
    libby 'org.bouncycastle:bcprov-jdk18on:1.76'
    libby 'org.apache.commons:commons-email:1.5'
    libby 'net.kyori:adventure-text-minimessage:4.14.0'
    libby 'com.github.kyngs:LegacyMessage:0.2.0'

    //Geyser
    compileOnly 'org.geysermc.floodgate:api:2.2.0-SNAPSHOT'

    //Bungeecord
    compileOnly 'net.md-5:bungeecord-api:1.20-R0.2-SNAPSHOT'
    compileOnly 'com.github.limework.redisbungee:RedisBungee-API:0.7.2'
    libby 'net.kyori:adventure-platform-bungeecord:4.1.2'

    //BStats
    libby 'org.bstats:bstats-velocity:3.0.2'
    libby 'org.bstats:bstats-bungeecord:3.0.2'
    libby 'org.bstats:bstats-bukkit:3.0.2'

    //Paper
    compileOnly 'io.papermc.paper:paper-api:1.20.2-R0.1-SNAPSHOT'
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.1.0'
    compileOnly 'io.netty:netty-transport:4.1.100.Final'
    compileOnly 'com.mojang:datafixerupper:5.0.28' //I hate this so much
    compileOnly 'org.apache.logging.log4j:log4j-core:2.21.1'

    //Libby
    implementation 'xyz.kyngs.libby:libby-bukkit:1.4.0'
    implementation 'xyz.kyngs.libby:libby-velocity:1.4.0'
    implementation 'xyz.kyngs.libby:libby-bungee:1.4.0'
    implementation 'xyz.kyngs.libby:libby-paper:1.4.0'

    //NanoLimboPlugin
    compileOnly 'com.github.bivashy.NanoLimboPlugin:api:1.0.8'
}

processResources {
    outputs.upToDateWhen { false }
    filesMatching('plugin.yml') {
        expand 'version': version
    }
    filesMatching('bungee.yml') {
        expand 'version': version
    }
    filesMatching('paper-plugin.yml') {
        expand 'version': version
    }
}
