plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'net.kyori.blossom' version '1.3.1'
    id 'java-library'
}

compileJava {
    options.encoding('UTF-8')
}

repositories {
    //mavenLocal()
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://repo.opencollab.dev/maven-snapshots/" }
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://hub.spigotmc.org/nexus/" }
    maven { url = "https://mvn.exceptionflug.de/repository/exceptionflug-public/" }
    maven { url = "https://repo.dmulloy2.net/repository/public/" }
    maven { url = "https://repo.alessiodp.com/releases/" }
    maven { url = "https://jitpack.io/" }
    //maven { url = "https://repo.kyngs.xyz/repository/maven-public/" }
}

blossom {
    replaceToken '@version@', version
}

shadowJar {
    archiveFileName = "LibreLogin.jar"

    dependencies {
        exclude(dependency('org.slf4j:.*:.*'))
        exclude(dependency('org.checkerframework:.*:.*'))
        exclude(dependency('com.google.errorprone:.*:.*'))
        exclude(dependency('com.google.protobuf:.*:.*'))
    }

    relocate 'co.aikar.acf', 'xyz.kyngs.librelogin.lib.acf'
    relocate 'com.github.benmanes.caffeine', 'xyz.kyngs.librelogin.lib.caffeine'
    relocate 'com.typesafe.config', 'xyz.kyngs.librelogin.lib.hocon'
    relocate 'com.zaxxer.hikari', 'xyz.kyngs.librelogin.lib.hikari'
    relocate 'org.mariadb', 'xyz.kyngs.librelogin.lib.mariadb'
    relocate 'org.bstats', 'xyz.kyngs.librelogin.lib.metrics'
    relocate 'org.intellij', 'xyz.kyngs.librelogin.lib.intellij'
    relocate 'org.jetbrains', 'xyz.kyngs.librelogin.lib.jetbrains'
    relocate 'io.leangen.geantyref', 'xyz.kyngs.librelogin.lib.reflect'
    relocate 'org.spongepowered.configurate', 'xyz.kyngs.librelogin.lib.configurate'
    relocate 'net.byteflux.libby', 'xyz.kyngs.librelogin.lib.libby'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

jar {
    from '../LICENSE.txt'
}


dependencies {
    //API
    implementation project(':API')

    //Velocity
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'
    compileOnly 'com.velocitypowered:velocity-proxy:3.0.1'

    //MySQL
    compileOnly 'org.mariadb.jdbc:mariadb-java-client:3.2.0'
    compileOnly 'com.zaxxer:HikariCP:5.0.1'

    //SQLite
    compileOnly 'org.xerial:sqlite-jdbc:3.43.0.0'

    //PostgreSQL
    compileOnly 'org.postgresql:postgresql:42.6.0'

    //ACF
    implementation 'com.github.kyngs.commands:acf-velocity:object-fix-SNAPSHOT'
    implementation 'com.github.kyngs.commands:acf-bungee:object-fix-SNAPSHOT'
    implementation 'com.github.kyngs.commands:acf-paper:object-fix-SNAPSHOT'

    //Utils
    compileOnly 'com.github.ben-manes.caffeine:caffeine:3.1.8'
    compileOnly 'org.spongepowered:configurate-hocon:4.1.2'
    compileOnly 'at.favre.lib:bcrypt:0.10.2'
    compileOnly 'dev.samstevens.totp:totp:1.7.1'
    compileOnly 'dev.simplix:protocolize-api:2.3.0'
    compileOnly 'org.bouncycastle:bcprov-jdk18on:1.76'
    compileOnly 'org.apache.commons:commons-email:1.5'
    implementation 'net.kyori:adventure-text-minimessage:4.14.0'
    implementation 'com.github.kyngs:LegacyMessage:0.2.0'

    //Geyser
    compileOnly 'org.geysermc.floodgate:api:2.2.0-SNAPSHOT'

    //Bungeecord
    compileOnly 'net.md-5:bungeecord-api:1.20-R0.2-SNAPSHOT'
    compileOnly 'com.github.limework.redisbungee:RedisBungee-API:0.7.2'

    //BStats
    implementation 'org.bstats:bstats-velocity:3.0.2'
    implementation 'org.bstats:bstats-bungeecord:3.0.2'
    implementation 'org.bstats:bstats-bukkit:3.0.2'

    //Paper
    compileOnly 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'
    compileOnly 'com.comphenix.protocol:ProtocolLib:5.1.0'
    compileOnly 'io.netty:netty-transport:4.1.97.Final'
    compileOnly 'com.mojang:datafixerupper:5.0.28' //I hate this so much
    compileOnly 'org.apache.logging.log4j:log4j-core:2.20.0'

    //Libby
    implementation 'com.github.kyngs.libby:libby-bukkit:relocator-fix-SNAPSHOT'
    implementation 'com.github.kyngs.libby:libby-velocity:relocator-fix-SNAPSHOT'
    implementation 'com.github.kyngs.libby:libby-bungee:relocator-fix-SNAPSHOT'
    implementation 'net.byteflux:libby-paper:1.3.0'

    //NanoLimboPlugin
    compileOnly 'com.github.bivashy.NanoLimboPlugin:api:1.0.6'

}

processResources {
    outputs.upToDateWhen { false }
    filesMatching('plugin.yml') {
        expand 'version': version
    }
    filesMatching('bungee.yml') {
        expand 'version': version
    }
    filesMatching('paper-plugin.yml') {
        expand 'version': version
    }
}
