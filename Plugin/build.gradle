plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'net.kyori.blossom' version '1.3.0'
}

version '0.5.4'

repositories {
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = "https://repo.opencollab.dev/maven-snapshots/" }
    maven { url = "https://nexus.velocitypowered.com/repository/maven-public/" }
    maven { url = "https://hub.spigotmc.org/nexus/" }
}

blossom {
    replaceToken '@version@', version
}

shadowJar {
    archiveFileName = "LibrePremium.jar"

    dependencies {
        exclude(dependency('org.slf4j:.*:.*'))
        exclude(dependency('org.checkerframework:.*:.*'))
        exclude(dependency('com.google.errorprone:.*:.*'))
        exclude(dependency('com.google.protobuf:.*:.*'))
    }

    relocate 'co.aikar', 'xyz.librepremium.lib.acf'
    relocate 'com.github.benmanes.caffeine', 'xyz.librepremium.lib.caffeine'
    relocate 'com.typesafe.config', 'xyz.librepremium.lib.hocon'
    relocate 'com.zaxxer.hikari', 'xyz.librepremium.lib.hikari'
    relocate 'com.mysql', 'xyz.librepremium.lib.mysql'
    relocate 'org.bstats', 'xyz.librepremium.lib.metrics'
    relocate 'org.intellij', 'xyz.librepremium.lib.intellij'
    relocate 'org.jetbrains', 'xyz.librepremium.lib.jetbrains'
    relocate 'io.leangen.geantyref', 'xyz.librepremium.lib.reflect'
    relocate 'org.spongepowered.configurate', 'xyz.librepremium.lib.configurate'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    //Velocity
    annotationProcessor 'com.velocitypowered:velocity-api:3.1.1'
    compileOnly 'com.velocitypowered:velocity-api:3.1.1'

    //MySQL
    implementation 'mysql:mysql-connector-java:8.0.28'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'com.github.kyngs:EasyDB:a4bdf88ee0'

    //ACF
    implementation 'co.aikar:acf-velocity:0.5.1-SNAPSHOT'
    implementation 'co.aikar:acf-bungee:0.5.1-SNAPSHOT'

    //Utils
    implementation 'com.github.ben-manes.caffeine:caffeine:3.0.6'
    implementation 'org.spongepowered:configurate-hocon:4.1.2'
    implementation 'at.favre.lib:bcrypt:0.9.0'

    //Geyser
    //compileOnly 'org.geysermc.floodgate:api:2.0'

    //Bungeecord
    compileOnly 'net.md-5:bungeecord-api:1.18-R0.1-SNAPSHOT'
    implementation 'net.kyori:adventure-platform-bungeecord:4.1.0'

    //BStats
    implementation 'org.bstats:bstats-velocity:3.0.0'
    implementation 'org.bstats:bstats-bungeecord:3.0.0'
}
